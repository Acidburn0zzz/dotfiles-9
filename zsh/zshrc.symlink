export ZSH=$HOME/workspace/src/github.com/chaseadamsio/dotfiles

export PROJECTS=$HOME/workspace/src/github.com/chaseadamsio

if [[ -a ~/.localrc ]] 
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [ -n "$TMUX" ]; then
  export TERM=screen-256color
else
  export TERM=xterm-256color
fi

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Base16 Shell
BASE16_SHELL="$HOME/.config/base16-shell/base16-ocean.dark.sh"
[[ -s $BASE16_SHELL  ]] && source $BASE16_SHELL

# nix path
NIX_LINK=$HOME/.nix-profile
export PATH=$NIX_LINK/bin:$NIX_LINK/sbin:$PATH

# Setting for the new UTF-8 terminal support in Lion
LC_CTYPE=en_US.UTF-8
LC_ALL=en_US.UTF-8

[[ -s "/usr/local/bin/nvim" ]] && alias vi="/usr/local/bin/nvim"

export GOPATH=$HOME/workspace
export WORKSPACE=$GOPATH/src
